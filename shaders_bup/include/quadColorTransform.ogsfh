////////////////////////////////////////////////////////////////////////////////////////////////////
// quadColorTransform.ogsfh (GLSL)
// Brief: Common color transformation snippets for MNPR
// Contributors: Amir Semmo, Pierre Bénard
////////////////////////////////////////////////////////////////////////////////////////////////////
//              _                _                        __                      
//     ___ ___ | | ___  _ __    | |_ _ __ __ _ _ __  ___ / _| ___  _ __ _ __ ___  
//    / __/ _ \| |/ _ \| '__|   | __| '__/ _` | '_ \/ __| |_ / _ \| '__| '_ ` _ \ 
//   | (_| (_) | | (_) | |      | |_| | | (_| | | | \__ \  _| (_) | |  | | | | | |
//    \___\___/|_|\___/|_|       \__|_|  \__,_|_| |_|___/_|  \___/|_|  |_| |_| |_|
//                                                                                
////////////////////////////////////////////////////////////////////////////////////////////////////
// This shader file provides color transformation functions
////////////////////////////////////////////////////////////////////////////////////////////////////
#ifndef _QUADCOLORTRANSFORM_FXH
#define _QUADCOLORTRANSFORM_FXH



GLSLShader Color_Transform_Functions {

//    ____   ____ ____       ____      _   _ ______     __
//   |  _ \ / ___| __ )     / /\ \    | | | / ___\ \   / /
//   | |_) | |  _|  _ \    / /  \ \   | |_| \___ \\ \ / / 
//   |  _ <| |_| | |_) |   \ \  / /   |  _  |___) |\ V /  
//   |_| \_\\____|____/     \_\/_/    |_| |_|____/  \_/   
//                                                        
  vec3 rgb2hsv(vec3 c)
  {
     vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
     //vec4 p = lerp(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
     //vec4 q = lerp(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));
     vec4 p = c.g < c.b ? vec4(c.bg, K.wz) : vec4(c.gb, K.xy);
     vec4 q = c.r < p.x ? vec4(p.xyw, c.r) : vec4(c.r, p.yzx);
     
     float d = q.x - min(q.w, q.y);
     float e = 1.0e-10;
     
     return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)), d / (q.x + e), q.x);
  }



//    ____   ____ ____       ____     __  ____   _______
//   |  _ \ / ___| __ )     / /\ \    \ \/ /\ \ / /__  /
//   | |_) | |  _|  _ \    / /  \ \    \  /  \ V /  / / 
//   |  _ <| |_| | |_) |   \ \  / /    /  \   | |  / /_ 
//   |_| \_\\____|____/     \_\/_/    /_/\_\  |_| /____|
//                                                      
  vec3 rgb2xyz(vec3 c) {
  	vec3 tmp;
  	tmp.x = (c.r > 0.04045) ? pow((c.r + 0.055) * 0.947867, 2.4) : c.r * 0.077399;
  	tmp.y = (c.g > 0.04045) ? pow((c.g + 0.055) * 0.947867, 2.4) : c.g * 0.077399;
  	tmp.z = (c.b > 0.04045) ? pow((c.b + 0.055) * 0.947867, 2.4) : c.b * 0.077399;
  	return 100.0 * mul(tmp, mat3(0.4124, 0.3576, 0.1805, 0.2126, 0.7152, 0.0722, 0.0193, 0.1192, 0.9505));
  }

  vec3 xyz2rgb(vec3 c) {
      vec3 v =  0.01 * mul(c, mat3(
                                 3.2406, -1.5372, -0.4986,
                                -0.9689,  1.8758,  0.0415,
                                 0.0557, -0.2040,  1.0570
                               ));
      vec3 r;
      r.x = ( v.r > 0.0031308 ) ? (( 1.055 * pow( v.r, 0.416667)) - 0.055 ) : 12.92 * v.r;
      r.y = ( v.g > 0.0031308 ) ? (( 1.055 * pow( v.g, 0.416667)) - 0.055 ) : 12.92 * v.g;
      r.z = ( v.b > 0.0031308 ) ? (( 1.055 * pow( v.b, 0.416667)) - 0.055 ) : 12.92 * v.b;
      return r;
  }



//   __  ____   _______     ____      _          _     
//   \ \/ /\ \ / /__  /    / /\ \    | |    __ _| |__  
//    \  /  \ V /  / /    / /  \ \   | |   / _` | '_ \ 
//    /  \   | |  / /_    \ \  / /   | |__| (_| | |_) |
//   /_/\_\  |_| /____|    \_\/_/    |_____\__,_|_.__/ 
//                                                     
  vec3 xyz2lab(vec3 c) {
  	vec3 n = c / vec3(95.047, 100, 108.883);
  	vec3 v;
  	v.x = (n.x > 0.008856) ? pow(n.x, 0.333333) : (7.787 * n.x) + 0.137931;
  	v.y = (n.y > 0.008856) ? pow(n.y, 0.333333) : (7.787 * n.y) + 0.137931;
  	v.z = (n.z > 0.008856) ? pow(n.z, 0.333333) : (7.787 * n.z) + 0.137931;
  	return vec3((116.0 * v.y) - 16.0, 500.0 * (v.x - v.y), 200.0 * (v.y - v.z));
  }

  vec3 lab2xyz(vec3 c) {
      float fy = ( c.x + 16.0 ) * 0.008620;
      float fx = c.y * 0.002 + fy;
      float fz = fy - c.z * 0.005;
      return vec3(
                  95.047 *  (( fx > 0.206897 ) ? fx * fx * fx : ( fx - 16.0 * 0.008620 ) * 0.128419),
                  100.000 * (( fy > 0.206897 ) ? fy * fy * fy : ( fy - 16.0 * 0.008620 ) * 0.128419),
                  108.883 * (( fz > 0.206897 ) ? fz * fz * fz : ( fz - 16.0 * 0.008620 ) * 0.128419)
                 );
  }



//    ____   ____ ____       ____      _          _     
//   |  _ \ / ___| __ )     / /\ \    | |    __ _| |__  
//   | |_) | |  _|  _ \    / /  \ \   | |   / _` | '_ \ 
//   |  _ <| |_| | |_) |   \ \  / /   | |__| (_| | |_) |
//   |_| \_\\____|____/     \_\/_/    |_____\__,_|_.__/ 
//                                                      
// Converts RGB <-> LAB colorspace via XYZ.
  vec3 rgb2lab(vec3 color) {
  	vec3 lab = xyz2lab(rgb2xyz(color));
  	return vec3(lab.x * 0.01, 0.5 + lab.y * 0.003937, 0.5 + lab.z * 0.003937);
  }

  vec3 lab2rgb(vec3 color) {
      return xyz2rgb(lab2xyz(vec3(100.0 * color.x, 2.0 * 127.0 * (color.y - 0.5), 2.0 * 127.0 * (color.z - 0.5)) ) );
  }




//    ____   ____ ____       ____      ______   ______  
//   |  _ \ / ___| __ )     / /\ \    |  _ \ \ / / __ ) 
//   | |_) | |  _|  _ \    / /  \ \   | |_) \ V /|  _ \ 
//   |  _ <| |_| | |_) |   \ \  / /   |  _ < | | | |_) |
//   |_| \_\\____|____/     \_\/_/    |_| \_\|_| |____/ 
//                                                      
//  Contributor: Pierre Bénard
//  Convert between RGB (red, green, blue) to RYB (red, yellow, blue)

  vec3 rgb2ryb(vec3 rgb_color) {
    // Remove the white from the color
    float white = min(min(rgb_color.r, rgb_color.g), rgb_color.b);
    rgb_color -= vec3(white);

    float max_green = max(max(rgb_color.r, rgb_color.g), rgb_color.b);

    // Get the yellow out of the red & green
    float yellow = min(rgb_color.r, rgb_color.g);
    rgb_color.r -= yellow;
    rgb_color.g -= yellow;

    // If this unfortunate conversion combines blue and green, then cut each in half to preserve the value's maximum range.
    if (rgb_color.b > 0. && rgb_color.g > 0.) {
        rgb_color.b /= 2.;
        rgb_color.g /= 2.;
    }

    // Redistribute the remaining green.
    yellow += rgb_color.g;
    rgb_color.b += rgb_color.g;

    // Normalize to values.
    float max_yellow = max(max(rgb_color.r, yellow), rgb_color.b);

    if (max_yellow > 0.) {
        float n = max_green / max_yellow;

        rgb_color.r *= n;
        yellow *= n;
        rgb_color.b *= n;
    }

    // Add the white back in.
    rgb_color.r += white;
    yellow += white;
    rgb_color.b += white;

    return vec3(rgb_color.r, yellow, rgb_color.b);
  }

  vec3 ryb2rgb(vec3 ryb_color) {
    // Remove the white from the color
    float white = min(min(ryb_color.r, ryb_color.g), ryb_color.b);
    ryb_color -= vec3(white);

    float max_yellow = max(max(ryb_color.r, ryb_color.g), ryb_color.b);

    // Get the green out of the yellow & blue
    float green = min(ryb_color.g, ryb_color.b);
    ryb_color.g -= green;
    ryb_color.b -= green;

    if (ryb_color.b > 0. && green > 0.) {
        ryb_color.b *= 2.;
        green *= 2.;
    }

    // Redistribute the remaining yellow.
    ryb_color.r += ryb_color.g;
    green += ryb_color.g;

    // Normalize to values.
    float max_green = max(max(ryb_color.r, green), ryb_color.b);

    if (max_green > 0.) {
        float n = max_yellow / max_green;

        ryb_color.r *= n;
        green *= n;
        ryb_color.b *= n;
    }

    // Add the white back in.
    ryb_color.r += white;
    green += white;
    ryb_color.b += white;

    return vec3(ryb_color.r, green, ryb_color.b);
  }
}
#endif /* _QUADCOLORTRANSFORM_FXH */
